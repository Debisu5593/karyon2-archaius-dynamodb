plugins {
    id 'java'
    id 'maven-publish'
    id 'pl.allegro.tech.build.axion-release' version '1.3.2'
}

scmVersion {
    ignoreUncommittedChanges = false
}

sourceCompatibility = 1.8

project.version = scmVersion.version

repositories {
    mavenLocal()
    maven {
        credentials {
            username project.hasProperty('artifactory_scm_user')? project.artifactory_scm_user: System.getenv()['ARTIFACTORY_SCM_USER']
            password project.hasProperty('artifactory_scm_password')? project.artifactory_scm_password: System.getenv()['ARTIFACTORY_SCM_PASSWORD']
        }

        url "http://artifactory.scmspain.com/artifactory/repo"
    }
    mavenCentral()
}


dependencies {
    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'com.netflix.karyon:karyon2-core:2.7.4'

    compile 'com.netflix.karyon:karyon2-archaius:2.7.4'
    compile 'com.netflix.archaius:archaius-aws:0.7.1'
    compile 'com.netflix.governator:governator-archaius:1.10.5'
}

test.doFirst {
    systemProperty 'archaius.deployment.environment', System.getProperty('archaius.deployment.environment', 'test')
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// Task publishMavenJavaPublicationToMavenRepository is added in runtime so doFirst doesn't work propertly and must be done this way
tasks.whenTaskAdded { t ->
    if( t.name == "publishMavenJavaPublicationToMavenRepository"){
        if (!project.hasProperty('artifactory_scm_user') || !project.hasProperty('artifactory_scm_password')) {
            throw new GradleException("Properties 'artifactory_scm_user' and 'artifactory_scm_password' are mandatory to do a publish, please add this information in your ~/.gradle/gradle.properties")
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.scmspain'
            version project.version

            from components.java

            artifact sourcesJar
            artifact javadocJar

        }
    }
    repositories {
        maven {
            credentials {
                username project.hasProperty('artifactory_scm_user')? artifactory_scm_user: System.getenv()['ARTIFACTORY_SCM_USER']
                password project.hasProperty('artifactory_scm_password')? artifactory_scm_password: System.getenv()['ARTIFACTORY_SCM_PASSWORD']
            }

            url "http://artifactory.scmspain.com/artifactory/libs-release-ms"
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}

